// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack
/**
 * A library that supports various program execution services.
 */
class Sys {
    static int delayFactor;
    
    /** Performs all the initializations required by the OS. */
    function void init() {
        let delayFactor = 500; // system dependent
        
        // Initialize OS classes
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Keyboard.init();
        do Output.init();
        
        // call main entry point
        do Main.main();
        do Sys.halt();
        return;
    }
    
    /** Halts the program execution. */
    function void halt() {
        while (true) {}
        return;
    }
    
    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int timeMS) {
        var int outerCount, innerCount;
        
        let outerCount = 0;
        while (outerCount < timeMS) {
            let innerCount = 0;
            while (innerCount < delayFactor) {
                let innerCount = innerCount + 1;
            }
            let outerCount = outerCount + 1;
        }
        return;
    }
    
    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorNumber) {
        do Output.printString("ERR");
        do Output.printInt(errorNumber);
        do Sys.halt();
        return;
    }
}